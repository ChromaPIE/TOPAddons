buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://maven.parchmentmc.org"}
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
        classpath group: "gradle.plugin.com.matthewprenger", name: "CurseGradle", version: "1.4.0"
        classpath group: "org.parchmentmc", name: "librarian", version: "1.+"
    }
}
apply plugin: "net.minecraftforge.gradle"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "org.parchmentmc.librarian.forgegradle"

version = "$mc_version-$MAJOR.$MINOR.$PATCH"
group = "io.github.drmanganese.topaddons"
archivesBaseName = "topaddons"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

minecraft {
    mappings channel: mcp_channel, version: mcp_mappings
    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        client {
            workingDirectory project.file("run")
            property "forge.logging.console.level", "debug"
            // Mixins in dev run env
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                topaddons {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run-server")
            property "forge.logging.console.level", "debug"
            mods {
                topaddons {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven { url = "https://maven.k-4u.nl" }
    maven { url = "https://www.cursemaven.com" }
    maven { url = "https://maven.blamejared.com" }
    maven { url = "https://cdn.hrzn.studio/maven/" }
    maven { url = "https://maven.covers1624.net/" }
}

dependencies {
    minecraft "net.minecraftforge:forge:$mc_version-$forge_version"
    implementation fg.deobf("mcjty.theoneprobe:theoneprobe:$top_version") {transitive = false}

    implementation fg.deobf("com.teamcofh:cofh_core:${mc_version}-10.2.0.+")
    implementation fg.deobf("com.teamcofh:thermal_core:${mc_version}-10.2.0.+")
    implementation fg.deobf("curse.maven:thermal-foundation-222880:4382373")
    implementation fg.deobf("curse.maven:thermal-expansion-69163:4382371")

    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:1.19.2-77")

    implementation fg.deobf("curse.maven:industrial-foregoing-266515:4401699")
    implementation fg.deobf("curse.maven:titanium-287342:4391554")

    implementation fg.deobf("curse.maven:powah-rearchitected-633483:4183078")
    runtimeOnly fg.deobf("curse.maven:architectury-api-419699:4384406")
    runtimeOnly fg.deobf("curse.maven:cloth-config-348521:3972423")
}

jar {
    manifest {
        attributes([
            "FMLAT": "accesstransformer.cfg",
            "Specification-Title": "topaddons",
            "Specification-Vendor": "DrManganese",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": getArchiveVersion(),
            "Implementation-Vendor" :"DrManganese",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    from "LICENSE"
}

curseforge {
    apiKey = curseforgeToken
    project {
        id = "247111"
        addGameVersion "1.18"
        changelogType = "markdown"
        changelog = file("CHANGELOG.md")
        releaseType = "beta"
        mainArtifact(jar) {
            relations {
                requiredDependency "the-one-probe"
            }
        }
    }
}

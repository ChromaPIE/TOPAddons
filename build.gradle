buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }

    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.0.9"
    id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.47"
}


apply plugin: "net.minecraftforge.gradle.forge"


repositories {
    mavenCentral()
    maven {
        name = "The One Probe"
        url = "http://maven.tterrag.com"
    }
    maven {
        name = "COFH, Codechicken"
        url "http://maven.covers1624.net/"
    }
    maven {
        name = "Forestry, ICÂ², Binnie's"
        url = "http://maven.ic2.player.to"
    }
    maven {
        name = "Tesla"
        url "http://maven.epoxide.xyz"
    }
    maven {
        name = "Tinkers', JEI"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "Blood Magic"
        url = "http://tehnut.info/maven/"
    }

    maven {
        name = "OpenComputers"
        url = "http://maven.cil.li/"
    }

    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
}

dependencies {
    deobfCompile("mcjty.theoneprobe:TheOneProbe-1.12:1.12-+") {
        transitive = false
    }
    deobfCompile("binnie:binnie-mods-1.12.2:2.5.0.115") {
        transitive = false
    }
    deobfCompile("slimeknights:TConstruct:1.12-+") {
        transitive = false
    }
    deobfCompile "net.sengir.forestry:forestry_1.12.2:5.7.0.214+"
    deobfCompile "net.industrial-craft:industrialcraft-2:2.8.9-ex112"
    deobfCompile "slimeknights.mantle:Mantle:1.12-+"
    deobfCompile "steves-carts-reborn:StevesCarts:1.12.2:2.4.10.49"
    deobfCompile "li.cil.oc:OpenComputers:MC1.12.1-1.7.+"
    compile "cofh:RedstoneFlux:1.12-2.0.1.2:universal"
    //Storage drawers, Botania, Chisel, CoFH+, Advanced Generators, Lycanite's Mobs
    compile fileTree(dir: 'libs', include: ['*.jar'])
}


sourceCompatibility = 1.8
targetCompatibility = 1.8

version = mc_version + '-' + MAJOR + '.' + MINOR + '.' + PATCH
group = "io.github.drmanganese"
archivesBaseName = "topaddons"

minecraft {
    version = mc_version + '-' + forge_version
    runDir = "run"
    mappings = mcp_mappings

    replace '@VERSION@', project.version
    useDepAts = true
}


processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from "LICENSE"
}

task generateGitChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    filePath = "CHANGELOG.md"
    fromRef = "v1.11.2-0.15.0"
    toRef = "HEAD"
    templateContent = file('changelog.mustache').getText('UTF-8')
    ignoreCommitsIfMessageMatches = "v.*-.*|Merge"
}

curseforge {
    apiKey = curseforgeToken
    project {
        id = "247111"
        changelogType = "markdown"
        changelog = file("CHANGELOG.md")
        releaseType = "release"
        mainArtifact jar
        addArtifact sourceJar
        relations {
            requiredLibrary "the-one-probe"
        }
    }
}

sourceSets {
    main {
        java {
            exclude '**/addons/AddonBloodMagic.java'
            exclude '**/addons/AddonNeoTech.java'
            exclude '**/elements/bloodmagic'
        }
    }
}

idea { module { inheritOutputDirs = true } }
